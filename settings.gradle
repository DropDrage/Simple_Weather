pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }

    def coroutinesVersion = '1.8.1'
    def navigationVersion = '2.7.7'
    def hiltVersion = '2.51.1'
    def tomlDirectory = 'gradle'
    versionCatalogs {
        buildLibs {
            version('navigation', navigationVersion)
            version('hilt', hiltVersion)
            from(files("$tomlDirectory/buildLibs.versions.toml"))
        }

        kotlinLibs {
            version('coroutines', coroutinesVersion)
            from(files("$tomlDirectory/kotlinLibs.versions.toml"))
        }

        androidLibs {
            version('navigation', navigationVersion)
            version('hilt', hiltVersion)
            version('coroutines', coroutinesVersion)
            from(files("$tomlDirectory/androidLibs.versions.toml"))
        }

        networkLibs {
            from(files("$tomlDirectory/networkLibs.versions.toml"))
        }

        testLibs {
            version('coroutines', coroutinesVersion)
            version('hilt', hiltVersion)
            from(files("$tomlDirectory/testLibs.versions.toml"))
        }
        localStorageLibs {
            from files('gradle/localStorageLibs.versions.toml')
        }
    }
}

rootProject.name = "Simple_Weather"

include ':app',
    ':adapters',

    ':common:data',
    ':common:domain',
    ':common:presentation',
    ':common:build_config_checks',
    ':common:test',
    ':common:util',

    ':core:domain',
    ':core:presentation',
    ':core:style',
    ':core:util',

    ':data:location',
    ':data:city',
    ':data:settings',
    ':data:weather',

    ':feature:city:domain',
    ':feature:city:list',
    ':feature:city:list:domain',
    ':feature:city:search',
    ':feature:city:search:domain',
    ':feature:settings',
    ':feature:weather',
    ':feature:weather:domain'
