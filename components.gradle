ext.component_retrofit = { Project project ->
    project.dependencies {
        implementation networkLibs.retrofit.core
        implementation networkLibs.moshi.kotlin
    }
}

ext.component_android_viewBinding = { Project project ->
    buildFeature_viewBinding(project)
    project.dependencies {
        implementation androidLibs.viewBinding
    }
}
ext.component_android_hilt = { Project project ->
    project.apply plugin: 'com.google.devtools.ksp'
    project.apply plugin: 'com.google.dagger.hilt.android'

    project.dependencies {
        implementation androidLibs.hilt.android
        ksp androidLibs.hilt.compiler
    }
}
ext.component_android_room = { Project project ->
    project.apply plugin: 'com.google.devtools.ksp'

    project.dependencies {
        implementation localStorageLibs.bundles.room
        ksp localStorageLibs.room.compiler
    }
}

ext.component_android_unitTests = { Project project ->
    project.android {
        testOptions.unitTests.all {
            useJUnitPlatform()
        }
    }

    project.dependencies {
        testImplementation testLibs.bundles.unit5
        testRuntimeOnly testLibs.junit5.runtime
    }
}
ext.component_android_uiTest = { Project project ->
    project.android {
        defaultConfig {
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
        }
    }

    project.dependencies {
        androidTestImplementation testLibs.bundles.instrumental5
        androidTestRuntimeOnly testLibs.junit5.android.test.runner
    }
}
