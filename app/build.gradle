plugins {
    alias(buildLibs.plugins.android.application)
    alias(buildLibs.plugins.kotlin.android)
    alias(buildLibs.plugins.ksp)

    alias(testLibs.plugins.junit5.android)
}

config_android(
    project,
    bundle_uiScreen,
    component_retrofit, component_android_unitTests,
    buildFeature_buildConfig,
    testOptions_unitTests_returnDefaultValues,
)

android {
    namespace 'com.dropdrage.simpleweather'

    defaultConfig {
        applicationId "com.dropdrage.simpleweather"

        versionCode project.versionCode
        versionName project.versionName
    }

    buildTypes {
        release {
            shrinkResources true

            ksp {
                arg("dagger.fullBindingGraphValidation", "ERROR")
            }
        }
    }

    lint {
        checkDependencies true

        baseline project.rootProject.file('lint-baseline.xml')
        warningsAsErrors true
    }
}

dependencies {
    implementation androidLibs.navigation.ui
    implementation androidLibs.services.location

    implementation androidLibs.work

    //Hilt
    implementation androidLibs.hilt.work.core
    ksp androidLibs.hilt.work.compiler

    //Moshi
    implementation networkLibs.bundles.moshi
    ksp networkLibs.moshi.compiler
    implementation networkLibs.logging.interceptor

    //Utils
    implementation localStorageLibs.kotpref.core

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    implementation projects.common.data, { because "SimpleRepository Hilt resolve" }
    implementation projects.common.presentation, { because "ChangeableAppBar" }
    implementation projects.core.style
    implementation projects.data.city
    implementation projects.data.location
    implementation projects.data.weather
    implementation projects.feature.city.list
    implementation projects.feature.city.list.domain
    implementation projects.feature.city.search.domain
    implementation projects.feature.settings
    implementation projects.feature.weather
    implementation projects.feature.weather.domain

    //Tests
    testImplementation testLibs.coroutines

    testImplementation testLibs.hilt.testing
    kspTest androidLibs.hilt.compiler
    androidTestImplementation testLibs.hilt.testing
    kspAndroidTest androidLibs.hilt.compiler

    testImplementation testFixtures(projects.common.test)
}
