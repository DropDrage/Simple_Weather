plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    alias(buildLibs.plugins.ksp)

    alias(testLibs.plugins.junit5.android)
}

apply from: import_android

apply from: component_retrofit
apply from: component_tests

apply from: build_feature_buildConfig
apply from: build_feature_resValues

android {
    namespace 'com.dropdrage.simpleweather'

    defaultConfig {
        applicationId "com.dropdrage.simpleweather"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ksp {
                arg("dagger.fullBindingGraphValidation", "ERROR")
            }
        }
    }
}

dependencies {
    implementation androidLibs.work

    //Hilt
    implementation androidLibs.hilt.work
    ksp androidLibs.hilt.work.compiler

    //Moshi
    implementation networkLibs.bundles.moshi
    ksp networkLibs.moshi.compiler
    implementation networkLibs.logging.interceptor

    //Utils
    implementation androidLibs.kotpref

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    implementation project(':core:style')
    implementation project(':common:data'), { because "SimpleRepository Hilt resolve" }
    implementation project(':common:presentation'), { because "ChangeableAppBar" }
    implementation project(':data:city')
    implementation project(':data:location')
    implementation project(':data:weather')
    implementation project(':feature:city:domain')
    implementation project(':feature:city:list:domain')
    implementation project(':feature:city:list:presentation')
    implementation project(':feature:city:search:domain')
    implementation project(':feature:settings:presentation')
    implementation project(':feature:weather:domain')
    implementation project(':feature:weather:presentation')

    //Tests
    testImplementation testLibs.coroutines

    androidTestImplementation androidLibs.hilt.testing
    kspAndroidTest androidLibs.hilt.compiler
    testImplementation androidLibs.hilt.testing
    kspTest androidLibs.hilt.compiler

    testImplementation testFixtures(project(':common:test'))
}
