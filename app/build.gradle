plugins {
    alias(buildLibs.plugins.android.application)
    alias(buildLibs.plugins.kotlin.android)
    alias(buildLibs.plugins.ksp)

    alias(testLibs.plugins.junit5.android)
}

config_android(
    project,
    bundle_uiScreen,
    component_retrofit, component_android_unitTests,
    buildFeature_buildConfig,
    testOptions_unitTests_returnDefaultValues,
)

android {
    namespace 'com.dropdrage.simpleweather'

    defaultConfig {
        applicationId "com.dropdrage.simpleweather"

        versionCode project.versionCode
        versionName project.versionName
    }

    buildTypes {
        release {
            shrinkResources true

            ksp {
                arg("dagger.fullBindingGraphValidation", "ERROR")
            }
        }
    }

    lint {
        checkDependencies true

        baseline project.rootProject.file('lint-baseline.xml')
        warningsAsErrors true
    }
}

dependencies {
    implementation androidLibs.navigation.ui
    implementation androidLibs.services.location

    implementation androidLibs.work

    //Hilt
    implementation androidLibs.hilt.work.core
    ksp androidLibs.hilt.work.compiler

    //Moshi
    implementation networkLibs.bundles.moshi
    ksp networkLibs.moshi.compiler
    implementation networkLibs.logging.interceptor

    //Utils
    implementation localStorageLibs.kotpref.core

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    implementation project(':common:data'), { because "SimpleRepository Hilt resolve" }
    implementation project(':common:presentation'), { because "ChangeableAppBar" }
    implementation project(':core:style')
    implementation project(':data:city')
    implementation project(':data:location')
    implementation project(':data:weather')
    implementation project(':feature:city:list')
    implementation project(':feature:city:list:domain')
    implementation project(':feature:city:search:domain')
    implementation project(':feature:settings')
    implementation project(':feature:weather')
    implementation project(':feature:weather:domain')

    //Tests
    testImplementation testLibs.coroutines

    testImplementation testLibs.hilt.testing
    kspTest androidLibs.hilt.compiler
    androidTestImplementation testLibs.hilt.testing
    kspAndroidTest androidLibs.hilt.compiler

    testImplementation testFixtures(project(':common:test'))
}
